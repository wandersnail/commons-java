apply plugin: 'java-library'
apply plugin: 'maven'
apply plugin: 'signing'

dependencies {
    compileOnly "com.squareup.retrofit2:retrofit:$retrofit2_version"
    compileOnly "io.reactivex.rxjava2:rxjava:$rxjava2_version"
    compileOnly "com.squareup.retrofit2:adapter-rxjava2:$retrofit2_version"
    compileOnly 'com.alibaba.fastjson2:fastjson2:2.0.19'
    compileOnly 'com.alibaba:fastjson:2.0.19'
    compileOnly "com.google.code.gson:gson:$gson_version"
}

sourceCompatibility = "8"
targetCompatibility = "8"

signing {
    sign configurations.archives
}

def lib_version = '2.2.1'
def user_name = 'wandersnail'
def lib_group = 'cn.wandersnail'
def lib_artifact = 'commons-java'
def lib_packaging = 'jar'
def lib_description = 'Java平台使用的公共库'
def lib_website = 'https://github.com/wandersnail/commons'
group = lib_group
version = lib_version
archivesBaseName = lib_artifact

task sourcesJar(type: Jar) {
    archiveClassifier.set('sources')
    from sourceSets.main.java.srcDirs
}

task javadocJar(type: Jar) {
    archiveClassifier.set('javadoc')
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

Properties properties = new Properties()
InputStream inputStream = project.rootProject.file('local.properties').newDataInputStream()
properties.load(inputStream)
def ossrhUsername = properties.containsKey('ossrhUsername') ? properties['ossrhUsername'] : ''
def ossrhPassword = properties.containsKey('ossrhPassword') ? properties['ossrhPassword'] : ''

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment {
                MavenDeployment deployment -> signing.signPom(deployment)
            }
            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }
            pom.project {
                name lib_artifact
                artifactId lib_artifact
                packaging lib_packaging
                description lib_description
                version lib_version
                url lib_website

                scm {
                    url lib_website
                    connection "scm:git:${lib_website}.git"
                    developerConnection "scm:git:${lib_website}.git"
                }

                licenses {
                    license {
                        name 'The Apache License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }

                developers {
                    developer {
                        id user_name
                        name 'Zeng Fansheng'
                        email 'bingmo977@qq.com'
                    }
                }
            }
        }
    }
}